#!/usr/bin/env coffee

http = require 'http'
sockjs = require 'sockjs'
optimist = require 'optimist'
path = require 'path'

{Vec2} = require 'justmath'
{GameMaster} = require '../lib/game'
constants = require '../lib/constants'

argv = optimist
  .options('p', alias: 'port')
  .argv

clients = {}
gm = new GameMaster()

socket = sockjs.createServer()
socket.on 'connection', (conn) ->
  console.log "Client #{ conn.id } connected"
  clients[conn.id] = conn
  conn.on 'close', ->
    console.log "Client #{ conn.id } disconnected"
    delete clients[conn.id]

update = ->
  gm.doRound()
  state = gm.getFullState()
  for id, conn of clients
    conn.write JSON.stringify ['state', state]

setInterval update, constants.GAME_SPEED

server = http.createServer()
socket.installHandlers server, prefix: '/socket'
server.listen argv.port, '0.0.0.0', ->
  console.log "Realm Server listening on #{ argv.port }"
  process.title = "#{ path.basename(__filename) } --port=#{ argv.port }"
server.on 'error', (err) ->
  console.error  "Realm Server error: #{ err }"
  process.exit 1
